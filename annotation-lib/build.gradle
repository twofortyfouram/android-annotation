apply plugin: 'android-library'
apply plugin: 'maven'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
}

android {
    compileSdkVersion Integer.parseInt(ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    resourcePrefix 'com_twofortyfouram_annotation_'

    defaultConfig {
        minSdkVersion Integer.parseInt(ANDROID_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(ANDROID_VERSION_CODE)
        versionName ANDROID_VERSION_NAME
        consumerProguardFiles 'proguard-consumer.txt'
    }
    
    buildTypes {
        debug {
            //testCoverageEnabled true
        }
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        def androidHome = System.getenv('ANDROID_HOME')
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files) + files("${androidHome}/platforms/${android.compileSdkVersion}/android.jar")
        options.linksOffline('http://developer.android.com/reference/', "${androidHome}/docs/reference")
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(id: MAVEN_ID, url: MAVEN_URL) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
            pom.groupId = MAVEN_GROUP
            pom.version = ANDROID_VERSION_NAME
            pom.artifactId = MAVEN_ARTIFACT_NAME
        }
    }
}

// Workaround for naming conflict between maven and android plugins
// http://stackoverflow.com/questions/18559932/gradle-maven-plugin-install-task-does-not-work-with-android-library-project
task installArchives(type: Upload) {
    description "Installs artifacts to mavenLocal."
    repositories.mavenInstaller {
        configuration = configurations.default
        pom.groupId = MAVEN_GROUP
        pom.artifactId = MAVEN_ARTIFACT_NAME
        pom.version = ANDROID_VERSION_NAME
    }
}
